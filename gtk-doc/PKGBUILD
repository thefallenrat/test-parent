# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.27
pkgrel=1
pkgdesc="Documentation tool for public library API"
url="https://www.gtk.org/gtk-doc/"
arch=(any)
license=(GPL FDL)
depends=(docbook-xsl docbook-xml source-highlight glib2-docs python-six)
makedepends=(dblatex git yelp-tools)
checkdepends=(bc gtk3)
optdepends=('dblatex: PDF support')
_commit=31c38e0a948b000ff3c62765759376bc40b73bbf  # tags/GTK_DOC_1_27^0
source=("git+https://git.gnome.org/browse/gtk-doc#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/GTK_DOC_//' -e 's/_/\./g' -e 's/-/+/g'
}

prepare() {
  cd $pkgname

  # Recover a file from the old version, because GStreamer/common needs it
  git checkout 1db161bd708cdfb88b362ea0b5d047034d9c3272 -- gtkdoc-common.pl.in

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  sed 's|@PERL@|/usr/bin/perl|g' gtkdoc-common.pl.in \
    | install -D /dev/stdin "$pkgdir/usr/share/gtk-doc/data/gtkdoc-common.pl"
}
