# $Id$
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Adam Walters <abwalters@abwalters.us>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Rémy Oudompheng <remy@archlinux.org>

pkgname=libatomic_ops
pkgver=7.4.8
pkgrel=1
pkgdesc='Provides semi-portable access to hardware provided atomic memory operations'
arch=(x86_64)
url='https://github.com/ivmai/libatomic_ops'
depends=(glibc)
license=(GPL2 MIT)
source=($url/releases/download/v$pkgver/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c405d5524b118c197499bc311b8ebe18f7fe47fc820f2c2fcefde1753fbd436a')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --disable-static --enable-shared
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 doc/LICENSING.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -rf "$pkgdir/usr/share/doc/"
}
