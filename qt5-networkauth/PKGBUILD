# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qt5-networkauth
_qtver=5.10.0
pkgver=${_qtver/-/}
pkgrel=2
arch=(x86_64)
url='http://qt-project.org/'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Network authentication module'
depends=(qt5-base)
groups=(qt qt5)
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
        qtbug-65422.patch::"https://github.com/qt/qtnetworkauth/commit/f6aedc74.patch")
sha256sums=('ecdafbe8fabce386f1aab2bcf6a0d9fd6a1a9083b1554645bc45267f7188d8d2'
            'fa69959a5c32b10f13bf0212def7470ffd9af5a1dc166da55e3b4746d5dc76bc')

prepare() {
  mkdir -p build

  cd ${_pkgfqn}
  patch -p1 -i ../qtbug-65422.patch # Fix QOauth1 tokenSecret return value
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
