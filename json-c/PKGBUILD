# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: congyiwu <congyiwu AT gmail DOT com>

pkgname=json-c
pkgver=0.13
pkgrel=1
pkgdesc='A JSON implementation in C'
url='https://github.com/json-c/json-c/wiki'
license=(MIT)
arch=('x86_64')
depends=('glibc')
source=('https://github.com/json-c/json-c/archive/json-c-0.13-20171207.tar.gz')
sha256sums=('26e642456caab38aa9459279b9712ffec52f751e9f46641d28461c244bd6bae6')

build() {
  cd $pkgname-$pkgname-$pkgver-20171207
  CFLAGS="$CFLAGS -Wno-error"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgname-$pkgver-20171207
  make check
}

package() {
  cd $pkgname-$pkgname-$pkgver-20171207
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
