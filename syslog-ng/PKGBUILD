# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=syslog-ng
pkgver=3.12.1
pkgrel=6
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities"
arch=('x86_64')
url="http://www.balabit.com/network-security/syslog-ng/"
license=('GPL2' 'LGPL2.1')
depends=('awk' 'eudev' 'glib2' 'libdbi')
makedepends=('python2' 'libxslt' 'json-c' 'libmongoc' 'librabbitmq-c' 'python')
checkdepends=('python-nose' 'python-ply')
optdepends=('logrotate: for rotating log files'
            'json-c: for json-plugin'
            'curl: for the HTTP module'
            'librabbitmq-c: for the AMQP plugin'
            'libmongoc: for the MongoDB plugin')
conflicts=('eventlog')
replaces=('eventlog')
backup=('etc/syslog-ng/scl.conf'
        'etc/syslog-ng/syslog-ng.conf'
        'etc/logrotate.d/syslog-ng')
source=(https://github.com/balabit/syslog-ng/releases/download/syslog-ng-$pkgver/$pkgname-$pkgver.tar.gz
        syslog-ng.conf syslog-ng.logrotate
        syslog-ng-json-c-0.13.patch::"https://github.com/balabit/syslog-ng/commit/1b824dd6.patch")
sha256sums=('12d2acb8e292d16068debae7f9a699e791ca497540d28833a694a9a1ccd78f43'
            '7c760108faf56cb645b1102e3a3363cc5943ff03d19021ae7ac623d268e7471a'
            '93c935eca56854011ea9e353b7a1da662ad40b2e8452954c5b4b5a1d5b2d5317'
            '6c1b29e18e502ca670b8f6e519fc6f5525fc384632ea3a036167d68b7f69ccc5')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../syslog-ng-json-c-0.13.patch # Fix build with json-c 0.13
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc/syslog-ng --libexecdir=/usr/lib \
    --sbindir=/usr/bin --localstatedir=/var/lib/syslog-ng --datadir=/usr/share \
    --with-pidfile-dir=/run --disable-spoof-source --enable-ipv6 --enable-sql \
    --disable-systemd --with-systemdsystemunitdir=no \
    --enable-manpages --with-jsonc=system --with-mongoc=system --with-librabbitmq-client=system
  make
}

check() {
  cd $pkgname-$pkgver
  make check || /bin/true # python test requires pep8
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/var/lib/syslog-ng" "$pkgdir/etc/syslog-ng/patterndb.d"
  install -Dm644 "$srcdir/syslog-ng.conf" "$pkgdir/etc/syslog-ng/syslog-ng.conf"
  install -Dm644 "$srcdir/syslog-ng.logrotate" "$pkgdir/etc/logrotate.d/syslog-ng"

# See http://lists.balabit.hu/pipermail/syslog-ng/2016-February/022667.html
  rm -r "$pkgdir"/usr/share/syslog-ng/include/scl/cim
}
