# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=desktop-file-utils
pkgver=0.23
pkgrel=1
pkgdesc="Command line utilities for working with desktop entries"
arch=('i686' 'x86_64')
url="https://www.freedesktop.org/wiki/Software/desktop-file-utils"
license=('GPL')
depends=('glib2')
source=(https://www.freedesktop.org/software/desktop-file-utils/releases/desktop-file-utils-${pkgver}.tar.xz{,.asc}
        update-desktop-database.hook)
sha256sums=('6c094031bdec46c9f621708f919084e1cb5294e2c5b1e4c883b3e70cb8903385'
            'SKIP'
            '2be5efa6c93f89691d106c086ef392e5dbf2839e2de2afc43a7f616f55b2a81d')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7') #Hans Petter Jansson <hpj@suse.com>

build() {
  cd "${srcdir}/desktop-file-utils-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/desktop-file-utils-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 "$srcdir"/update-desktop-database.hook "$pkgdir"/usr/share/libalpm/hooks/update-desktop-database.hook
}
