# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gcr
pkgver=3.20.0+55+g470bf4c
pkgrel=1
pkgdesc="A library for bits of crypto UI and parsing"
url="https://git.gnome.org/browse/gcr"
arch=(i686 x86_64)
license=(GPL2)
depends=(dconf gtk3 libgcrypt p11-kit)
makedepends=(intltool gobject-introspection python vala libxslt gnome-common git)
checkdepends=(python2)
_commit=470bf4c073de6d2718c84b00f0bc4bd4812160ad  # master
source=("git+https://git.gnome.org/browse/gcr#commit=$_commit"
        10-gcr.conf)
sha256sums=('SKIP'
            '5f2eda7175ae9f23ee0e09d2beceb24fd2f6daafd7bddfcc1c1f5a3734eb60fc')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir path
  ln -s /usr/bin/python2 path/python
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/gcr \
    --enable-gtk-doc \
    --disable-static \
    --disable-update-mime \
    --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  PATH="$srcdir/path:$PATH" dbus-run-session make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  # gcr wants to lock some memory to prevent swapping out private keys
  # https://bugs.archlinux.org/task/32616
  # https://bugzilla.gnome.org/show_bug.cgi?id=688161
  install -Dm644 ../10-gcr.conf "$pkgdir/etc/security/limits.d/10-gcr.conf"
}

# vim:set ts=2 sw=2 et:
