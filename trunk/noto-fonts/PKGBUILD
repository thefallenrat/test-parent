# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=noto-fonts
pkgname=(noto-fonts noto-fonts-extra ttf-croscore)
pkgver=20171025
_commit=8ef14e6c606a7a0ef3943b9ca01fd49445620d79  # master
pkgrel=2
pkgdesc="Google Noto TTF fonts"
arch=(any)
url="https://www.google.com/get/noto/"
license=(custom:SIL)
provides=(ttf-font)
source=("$pkgbase-$_commit.tar.gz::https://github.com/googlei18n/noto-fonts/archive/$_commit.tar.gz"
        66-noto-sans.conf 66-noto-serif.conf 66-noto-mono.conf)
sha256sums=('cfb311dbaf1e0c4f3681e671986273430e3cdc5146abcf8cbf9c8e44f9e5f317'
            '65ff237a579d438eb1b1c90bf25b5818e15ab019dbbfed2fc283b9cd12e5177f'
            '27419c0557d05e13edbb244cd06a62d921c271c6c2f07126856519869035776e'
            '8c541eab8b44f6c7b02e981a4fbbd082eb6c1aa8285e11dede5f0a3ec0617772')

package_noto-fonts() {
  optdepends=('noto-fonts-cjk: CJK characters' 'noto-fonts-emoji: Emoji characters'
              'noto-fonts-extra: additional variants (condensed, semi-bold, extra-light)')

  cd $pkgbase-*/
  install -Dm644 unhinted/Noto*.tt[fc] -t "$pkgdir"/usr/share/fonts/noto
  install -Dm644 hinted/Noto*.tt[fc] -t "$pkgdir"/usr/share/fonts/noto
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/noto-fonts

  # Already in NotoSans-*.ttc
  rm "$pkgdir"/usr/share/fonts/noto/NotoSans-{Bold,BoldItalic,Italic,Regular}.ttf
  # Move to noto-fonts-extra
  rm -f "$pkgdir"/usr/share/fonts/noto/Noto*{Condensed,SemiBold,Extra}*.ttf

  # Install fontconfig files
  install -Dm644 "$srcdir"/*.conf -t "$pkgdir"/etc/fonts/conf.avail/
}

package_noto-fonts-extra() {
  pkgdesc+=' - additional variants'
  depends=(noto-fonts)
  
  cd $pkgbase-*/
  mkdir -p "$pkgdir"/usr/share/fonts/noto
  cp hinted/Noto*{Condensed,SemiBold,Extra}*.tt[fc] "$pkgdir"/usr/share/fonts/noto
}

package_ttf-croscore() {
  pkgdesc='Chrome OS core fonts'

  cd $pkgbase-*/
  install -Dm644 hinted/{Arimo,Cousine,Tinos}*.ttf -t "$pkgdir"/usr/share/fonts/TTF
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/ttf-croscore
}
