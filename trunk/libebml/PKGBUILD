# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: 03/08/04 <lefungus@altern.org>

pkgname=libebml
pkgver=1.3.5
pkgrel=1
pkgdesc='Extensible Binary Meta Language library'
arch=('i686' 'x86_64')
url='https://github.com/Matroska-Org/libebml'
license=('LGPL')
depends=('gcc-libs')
provides=('libebml.so')
source=("libebml-${pkgver}.tar.gz::https://github.com/Matroska-Org/libebml/archive/release-${pkgver}.tar.gz")
sha256sums=('efe9851797db47bc78486b6c518d822cfca435afe3d9c9d0812d8e3825cd71e6')

prepare() {
  cd libebml-release-${pkgver}

  autoreconf -fiv
}

build() {
  cd libebml-release-${pkgver}

  ./configure \
    --prefix='/usr'
  make
}

package() {
  cd libebml-release-${pkgver}

  make prefix="${pkgdir}/usr" install
}

# vim: ts=2 sw=2 et:
