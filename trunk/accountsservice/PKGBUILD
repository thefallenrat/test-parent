# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

_commit=db223bc474b5ae914f2c6ff4d07c69d747cfeda7  # tags/0.6.45^0

pkgname=accountsservice
pkgver=0.6.45
pkgrel=1
pkgdesc="D-Bus interface for user account query and manipulation"
url="https://www.freedesktop.org/software/accountsservice/"
arch=('i686' 'x86_64')
license=('GPL3')
# groups=('elogind-nosystemd')
# provides=('accountsservice')
# conflicts=('accountsservice')
depends=('glib2' 'elogind' 'polkit')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection' 'git')
source=("git://anongit.freedesktop.org/accountsservice#commit=$_commit"
        'accountsservice-enable-elogind.patch')
sha256sums=('SKIP'
            'dc233c540cf3e129479aa0c5e408a706a8c8cd52fdb94ad50aba32f8e9f765e1')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/-/+/g'
}


prepare() {
    cd $pkgname
    patch -Np 1 -i ${srcdir}/accountsservice-enable-elogind.patch
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd $pkgname
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/$pkgname \
        --disable-static \
        --enable-elogind \
        --enable-admin-group="wheel" \
        --disable-systemd \
        --enable-gtk-doc
    make
}

package() {
    cd $pkgname
    make DESTDIR="$pkgdir" install
}
