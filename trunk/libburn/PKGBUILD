# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=libburn
pkgver=1.4.8 # .pl01
pkgrel=1
pkgdesc="Library for reading, mastering and writing optical discs"
arch=('i686' 'x86_64')
url="https://dev.lovelyhq.com/libburnia"
license=('GPL')
depends=('glibc')
source=(http://files.libburnia-project.org/releases/${pkgname}-${pkgver}.tar.gz{,.sig})
options=('!emptydirs')
sha256sums=('3e81a2e359376c38d96239a9c9967be715f706d150d89c337de0fc85ecb79da6'
            'SKIP')
validpgpkeys=('44BC9FD0D688EB007C4DD029E9CBDFC0ABC0A854') # Thomas Schmitt <scdbackup@gmx.net>

build() {
  cd ${pkgname}-${pkgver/.pl01/}
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver/.pl01/}
  make DESTDIR=${pkgdir} install
}

