# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=volume_key
pkgver=0.3.9
pkgrel=5
pkgdesc="A library for manipulating storage volume encryption keys and storing them separately from volumes to handle forgotten passphrases"
arch=('x86_64')
url="https://pagure.io/volume_key"
license=('GPL')
depends=('cryptsetup' 'nss' 'gpgme' 'python2')
makedepends=('swig')
source=("https://github.com/felixonmars/volume_key/archive/volume_key-$pkgver.tar.gz"
        '0001-Stop-using-crypt_get_error.patch')
sha512sums=('f013cecee09429a1bcabcce0ec1e9c531fa396a1233f90f1f09272dd8a2e3a0e013eb2f3fb5c215abb15225445feb05cee175a06f06ae5e0220cc953ab80a027'
            'a24939f1c208fe8b1b3e3ffc2b867b6a2ea9687a5a0daec808d191fc5366d3b5fadc8db2079bb3925df96a1d8c81cfae58db2725eb8f96a0350323020663c9fc')

prepare() {
  cd "$srcdir"/volume_key-volume_key-$pkgver

  # Stop using crypt_get_error
  patch -Np1 < "${srcdir}/0001-Stop-using-crypt_get_error.patch"
  
  autoreconf -i

  find . -name ".pyc" -delete
}

build() {
  cd "$srcdir"/volume_key-volume_key-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/volume_key-volume_key-$pkgver
  make DESTDIR="$pkgdir" install
  sed -i '/#include <config.h>/d' "$pkgdir"/usr/include/volume_key/libvolume_key.h
}
