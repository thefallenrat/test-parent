# $Id$
# Maintainer:  Tom Gundersen <teg@jklm.no>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=libogg
pkgver=1.3.3
pkgrel=2
pkgdesc='Ogg bitstream and framing library'
arch=('i686' 'x86_64')
url='http://www.xiph.org/ogg/'
license=('BSD')
depends=('glibc')
source=("https://downloads.xiph.org/releases/ogg/libogg-${pkgver}.tar.xz"
        "config_types.h")
sha256sums=('4f3fc6178a533d392064f14776b23c397ed4b9f48f5de297aba73b643f955c08'
            'c8ae64fa07700f97031d48fa0234c900c86fd8be9adfcf47c5599daa2e7f3a27')

build() {
  cd libogg-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd libogg-${pkgver}

  make DESTDIR="${pkgdir}" install

# install BSD license
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/libogg/

# Resolve multilib conflict
  mv "${pkgdir}"/usr/include/ogg/config_types{,-64}.h
  install -m 644 ../config_types.h -t "${pkgdir}"/usr/include/ogg/
}

# vim: ts=2 sw=2 et:
