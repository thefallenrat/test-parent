# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=dbus-python
pkgname=('python-dbus' 'python2-dbus' 'python-dbus-common')
pkgver=1.2.4
pkgrel=2
pkgdesc="Python bindings for DBUS"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
url="http://www.freedesktop.org/wiki/Software/DBusBindings"
makedepends=('docutils' 'dbus-glib' 'python' 'python2' 'autoconf-archive')
source=(http://dbus.freedesktop.org/releases/${pkgbase}/${pkgbase}-${pkgver}.tar.gz{,.asc})
md5sums=('7372a588c83a7232b4e08159bfd48fe5'
         'SKIP')
validpgpkeys=('3C8672A0F49637FE064AC30F52A43A1E4B77B059') #SimonMcVittie

prepare() {
  mkdir build-python2 build-python3
  cd $pkgbase-$pkgver
  autoreconf -fi
}

build() {
  cd build-python2
  PYTHON=python2 ../${pkgbase}-${pkgver}/configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

  cd ../build-python3
  ../${pkgbase}-${pkgver}/configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_python-dbus-common() {
  pkgdesc="Common dbus-python files shared between python-dbus and python2-dbus"
  conflicts=('dbus-python<1.1.0')

  cd build-python3
  make DESTDIR="${pkgdir}" install-data
  rm -rf "${pkgdir}"/usr/lib/python*
}

package_python-dbus() {
  depends=('python-dbus-common' 'python' 'dbus')
  optdepends=('dbus-glib: glib mainloop support')
  pkgdesc="Python 3.6 bindings for DBUS"

  cd build-python3
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/share"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
}

package_python2-dbus() {
  depends=('python-dbus-common' 'python2' 'dbus')
  optdepends=('dbus-glib: glib mainloop support')
  pkgdesc="Python 2.7 bindings for DBUS"
  replaces=('dbus-python')
  conflicts=('dbus-python')
  provides=("dbus-python=$pkgver")

  cd build-python2
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/usr/share"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
}
