# Maintainer: Chris Cromer <chris@cromer.cl>

pkgbase=runit
pkgname=('runit' 'runit-doc')
pkgver=2.1.2
pkgrel=1
pkgdesc='runit is a cross-platform Unix init scheme with service supervision'
arch=('x86_64')
url='http://smarden.org/runit/'
license=('BSD3')
source=("http://smarden.org/${pkgname}/${pkgbase}-${pkgver}.tar.gz")
md5sums=('6c985fbfe3a34608eb3c53dc719172c4')

prepare() {
	# set default service path to /run/runit/service
	cd "${srcdir}/admin/${pkgbase}-${pkgver}"
	sed -i 's|"/service/"|"/run/runit/service/"|' src/sv.c
}

build() {
	cd "${srcdir}/admin/${pkgbase}-${pkgver}"

	CFLAGS="${CFLAGS} -static"
	LDFLAGS="${LDFLAGS} -static"

	package/compile
}

check() {
	cd "${srcdir}/admin/${pkgbase}-${pkgver}"
	package/check
}

package_runit() {
	depends=('bash' 'runit-init' 'util-linux')
	optdepends=('runit-doc: documentation')
	groups=('init')
	# commands
	mkdir -p "${pkgdir}/usr/bin"
	install -Dm755 ${srcdir}/admin/${pkgbase}-${pkgver}/command/* "${pkgdir}/usr/bin"

	# man pages
	mkdir -p "${pkgdir}/usr/share/man/man8"
	install -Dm644 ${srcdir}/admin/${pkgbase}-${pkgver}/man/* "${pkgdir}/usr/share/man/man8"

	# default directories
	mkdir -p "$pkgdir/etc/runit/runsvdir/default"

	# license
	install -Dm644 "${srcdir}/admin/${pkgbase}-${pkgver}/package/COPYING" "$pkgdir/usr/share/licenses/runit/COPYING"
}

package_runit-doc() {
	groups=('init-doc')
	arch=('any')
	install -v -m755 -d "${pkgdir}/usr/share/doc"
	cp -r "${srcdir}/admin/${pkgbase}-${pkgver}/doc" "${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}"
	rm "${pkgdir}/usr/share/doc/${pkgbase}-${pkgver}/debian"
}
