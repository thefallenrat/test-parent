# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libnotify
pkgver=0.7.7
pkgrel=1
pkgdesc="Library for sending desktop notifications"
arch=(i686 x86_64)
url="https://developer.gnome.org/notification-spec/"
license=(LGPL)
depends=(gdk-pixbuf2)
makedepends=(gtk3 gobject-introspection git gnome-common)
_commit=c954cabaad9e224c0c6129032d824012d332c11b  # tags/0.7.7^0
source=("git://git.gnome.org/libnotify#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-static --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
