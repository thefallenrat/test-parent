# Maintainer: Chris Cromer <cromer@artixlinux.org>

pkgname=pamac-classic
pkgver=6.6.3
pkgrel=1
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64')
url="https://github.com/cromnix/pamac-classic"
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3>=0.38' 'gtk3>=3.22'
         'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'pacman<5.1' 'gnutls>=3.4')
makedepends=('gettext' 'itstool' 'vala>=0.36' 'cmake')
conflicts=('pamac' 'pamac-classic-tray-appindicator')
optdepends=('polkit-gnome: needed for authentification in Cinnamon, Gnome'
			'mate-polkit: needed for authentification in Mate'
			'lxsession: needed for authentification in Xfce, LXDE etc.'
			'pamac-tray-appindicator: tray icon for KDE')
source=("pamac-classic-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
install=pamac-classic.install

sha256sums=('74518cd35347972df10b63a5a107869374f8c027bb25a5aaeb2c8d51b40a7e50')

prepare() {
	[ -d "${srcdir}"/build ] && rm -rf build
	mkdir "${srcdir}"/build
}

build() {
	cd "${srcdir}"/build
	cmake "${srcdir}/pamac-classic-${pkgver}" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib \
		-DCMAKE_INSTALL_SYSCONFDIR=/etc \
		-DICON_UPDATE=OFF \
		-DDISABLE_AUR=ON
	make
}

package() {
	cd "${srcdir}"/build
	make DESTDIR="$pkgdir" install
	rm -rf "${pkgdir}/etc/systemd"
	rm -rf "${pkgdir}/usr/lib/systemd"
}
