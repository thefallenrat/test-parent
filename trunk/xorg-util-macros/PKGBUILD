# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-util-macros
pkgver=1.19.1
pkgrel=1
pkgdesc="X.Org Autotools macros"
arch=(any)
license=('custom')
url="https://xorg.freedesktop.org/"
source=(${url}/releases/individual/util/util-macros-${pkgver}.tar.bz2{,.sig})
sha256sums=('18d459400558f4ea99527bc9786c033965a3db45bf4c6a32eefdc07aa9e306a6'
            'SKIP')
validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A')

build() {
  cd util-macros-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd util-macros-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/lib"
  mv "${pkgdir}/usr/share/pkgconfig" "${pkgdir}/usr/lib/"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
