# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Vlad M. <vlad@archlinux.net>
# Contributor: Patrice Peterson <runiq at archlinux dot us>
# Contributor: Vivien Didelot <vivien+aur@didelot.org>

pkgname=i3blocks
pkgver=1.4
pkgrel=5
pkgdesc='Define blocks for your i3bar status line'
arch=('i686' 'x86_64')
groups=('i3')
url="https://github.com/vivien/i3blocks"
license=('GPL3')
optdepends=('acpi: For battery script'
            'bc: For bandwidth script'
            'lm_sensors: For temperature script'
            'openvpn: For openvpn script'
            'playerctl: For mediaplayer script'
            'alsa-utils: For volume script'
            'sysstat: For cpu_usage script'
            'dash: For date script'
            'gsimplecal: For date calendar')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/vivien/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        # additional blocks
        'date' 'key_layout'
        )
sha512sums=('c1f5abe90426e9f6284ef74001c9f50d08ce596b7bb8a284232116774375fa50168567785999266c8774fa39a1243c73f699a3e3be4440fbe16f33f0fde5cded'
            '03836aade790fc7ac6a62c2e41a767c828a2c2b76776684143507002a68619d00a90cb42196faabb0aa82bb53e4b11a01557455cfaf7b0c716bd0eca52bcae40'
            '956d4d22c8125f1083e429378f1b88f1993ef7e96b197d6648fd3d6c5e01d990aade9d9e40768e095d8b6cd970aba3e563c94cb1bc09dda4b7e392ab3cd329ab')
backup=('etc/i3blocks.conf')

build () {
  cd "${pkgname}-${pkgver}"
  make VERSION="${pkgver}" PREFIX=/usr
}

package () {
  cd "${pkgname}-${pkgver}"
  make VERSION="${pkgver}" PREFIX=/usr LIBEXECDIR=/usr/lib DESTDIR="${pkgdir}" install

  # add additional blocks
  install -m755 "$srcdir/date" "${pkgdir}/usr/lib/i3blocks/"
  install -m755 "$srcdir/key_layout" "${pkgdir}/usr/lib/i3blocks/"
}
