# $Id$
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>

pkgname=zziplib
pkgver=0.13.67
pkgrel=1
pkgdesc="A lightweight library that offers the ability to easily extract data from files archived in a single zip file"
arch=('i686' 'x86_64')
url="http://zziplib.sourceforge.net"
license=('LGPL' 'MPL')
depends=('zlib')
makedepends=('python2' 'xmlto' 'zip')
source=($pkgname-$pkgver.tar.gz::"https://github.com/gdraheim/zziplib/archive/v$pkgver.tar.gz")
sha256sums=('1278178bdabac832da6bbf161033d890d335a2e38493c5af553ff5ce7b9b0220')
sha512sums=('a34b801a18a2051aa3898a572508ffd327521b69878413af679b10f6a68b37e770651884ae611bf9c01ce14013c6a1e06adeadd3ef6219d4b9278f1b9e7a6459')

build() {
  cd ${pkgname}-${pkgver}
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

# fix permissions
  chmod -s "${pkgdir}/usr/share/man/man3"
# chmod 644 "${pkgdir}"/usr/share/man/man3/*
  chown -R root:root "${pkgdir}/usr/share/man/man3"
}

# vim: set ts=2 sw=2 et:
