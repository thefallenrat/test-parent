# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=babl
pkgver=0.1.38
pkgrel=1
pkgdesc="Dynamic, any to any, pixel format conversion library"
arch=('x86_64')
url="http://gegl.org/babl/"
license=('LGPL3')
depends=('glibc')
makedepends=('gobject-introspection' 'vala' git)
options=('!makeflags')
_commit=04cea9206deb0a6fb52b0a86012d70551b020205  # tags/BABL_0_1_38^0
source=("git+https://git.gnome.org/browse/babl#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^BABL_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/$pkgname"
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "${srcdir}/$pkgname"
  make check
}

package() {
  cd "${srcdir}/$pkgname"
  make DESTDIR="${pkgdir}" install
}
