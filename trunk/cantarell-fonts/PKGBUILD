# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cantarell-fonts
pkgver=0.0.25
pkgrel=3
epoch=1
pkgdesc="Cantarell font family"
url="https://git.gnome.org/browse/cantarell-fonts"
arch=(any)
license=('custom:SIL')
makedepends=(fontconfig)
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:3}/$pkgname-${pkgver}.tar.xz)
sha256sums=('14a228aa0b516dfc367b434a850f955a00c57fc549cbb05348e2b150196a737f')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-configdir=/etc/fonts/conf.avail
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install

  mkdir "$pkgdir/etc/fonts/conf.d"
  ln -srt "$pkgdir/etc/fonts/conf.d" "$pkgdir"/etc/fonts/conf.avail/*

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
