# Maintainer: artoo <artoo@manjaro.org>
# Contributor: Philip MÃ¼ller <philm|manjaro|org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>

_spkgrel=6
_repo=http://mirror.netcologne.de/archlinux/core/os

pkgbase=systemdcompat
pkgname=('systemd-dummy' 'libsystemd-dummy')
pkgver=233
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.freedesktop.org/wiki/Software/systemd"
source_i686=("$_repo/i686/libsystemd-$pkgver-${_spkgrel}-i686.pkg.tar.xz"
        "$_repo/i686/systemd-$pkgver-${_spkgrel}-i686.pkg.tar.xz")
source_x86_64=("$_repo/x86_64/libsystemd-$pkgver-${_spkgrel}-x86_64.pkg.tar.xz"
        "$_repo/x86_64/systemd-$pkgver-${_spkgrel}-x86_64.pkg.tar.xz")
sha256sums_i686=('3b698e53ccc6fe2995f2b95b1db3998e6cc8cdd2807d4097ed83b446f57d6130'
                 'a147668286943e7c988802ae06a5bb4c177b5f201dbcf31b72e94e0d838735f2')
sha256sums_x86_64=('d71294a729b026f8647f968bb8b6234f0baffd1a187f0ae537006df5aa1cedff'
                   '9b5fe69e15483c504712ef373dbdc251230e9ee552bfb88069567ab119da939f')

package_systemd-dummy() {
    pkgdesc="systemd-sysuser and systemd-tmpfiles binary; systemd compatibility package"
    provides=("systemd=${pkgver}" "systemd-tools=${pkgver}")
    depends=('eudev' 'libseccomp')
    conflicts=('systemd-tools' 'systemd' 'eudev-systemdcompat')

    install -d "$pkgdir"/usr/lib/systemd
    for f in usr/lib/systemd/libsystemd-shared*.so*;do
        mv -v $f $pkgdir/usr/lib/systemd
    done
}

package_libsystemd-dummy() {
    pkgdesc="systemd client libraries without udev; systemd compatibility package"
    provides=("libsystemd=${pkgver}" 'libsystemd.so')
    depends=('glibc' 'libcap' 'libgcrypt' 'lz4' 'xz' 'libeudev')
    conflicts=('libsystemd' 'eudev-systemdcompat')

    install -d $pkgdir/usr/lib
    for f in usr/lib/libsystemd*.so*;do
        mv -v $f $pkgdir/usr/lib
    done
}
