# Maintainer: Chris Cromer <chris@cromer.cl>

pkgname=runit-artix
pkgver=20171208
pkgrel=1
pkgdesc='Runit initscripts for Artix'
provides=('runit-init')
url='https://github.com/artix-linux/runit-artix/'
license=('custom:CC0')
arch=('i686' 'x86_64')
depends=('procps-ng' 'awk' 'sh' 'eudev')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/artix-linux/runit-artix/archive/${pkgver}.tar.gz")
backup=('etc/runit/rc.conf'
        'etc/runit/rc.local'
        'etc/runit/rc.shutdown')
sha256sums=('d6fc33bd4ac17779cf84be588c3115a91a1de830b59343b94337d1ff11a67d91')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" PREFIX="/usr" install

	# License
	mkdir -p "${pkgdir}"/usr/share/licenses/runit-artix
	sed -n '/To the extent/,/0/p' README.md > "${pkgdir}"/usr/share/licenses/runit-artix/LICENSE
}
