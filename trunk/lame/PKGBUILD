# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor:	Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=lame
pkgver=3.100
pkgrel=1
pkgdesc="A high quality MPEG Audio Layer III (MP3) encoder"
arch=('i686' 'x86_64')
url="http://lame.sourceforge.net/"
depends=('ncurses')
makedepends=('nasm')
license=('LGPL')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('83e260acbe4389b54fe08e0bdbf7cddb')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
              --enable-nasm \
              --enable-shared
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
