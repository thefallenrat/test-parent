# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=slang
pkgver=2.3.1a
pkgrel=1
pkgdesc="S-Lang is a powerful interpreted language"
arch=('i686' 'x86_64')
url="http://www.jedsoft.org/slang/"
license=('GPL')
depends=('pcre')
backup=('etc/slsh.rc')
options=('!makeflags')
source=(http://www.jedsoft.org/releases/slang/${pkgname}-${pkgver}.tar.bz2{,.asc})
sha1sums=('a8ea7f1b5736160a94efb67b137a0f5b9916bdf2'
          'SKIP')
validpgpkeys=('AE962A02D29BFE4A4BB2805FDE401E0D5873000A')  # John E. Davis

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd ${pkgname}-${pkgver}

  [[ $CARCH = "i686" ]] || make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install-all
}
