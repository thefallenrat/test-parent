# Maintainer: artoo <artoo@manjaro.org>
# Contributor: kozec <kozec at kozec.com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=polkit
pkgver=0.113+34+g29ba7af
pkgrel=1
pkgdesc="PolyciKit with elogind support for non-systemd systems"
arch=('i686' 'x86_64' 'armv7h')
url="http://www.freedesktop.org/wiki/Software/polkit"
license=('LGPL')
# provides=("polkit=$pkgver")
depends=('glib2' 'pam' 'expat' 'js' 'elogind')
makedepends=('intltool' 'gtk-doc' 'gobject-introspection' 'git' 'autoconf-archive' 'libelogind')
# conflicts=('polkit-consolekit')
#replaces=('polkit-consolekit')
options=('!libtool')
_commit=29ba7afba1b79a325183a71966f35926dfdf506e  # master
#source=("http://www.freedesktop.org/software/polkit/releases/$pkgname-$pkgver.tar.gz")
source=("git+https://anongit.freedesktop.org/git/polkit#commit=$_commit"
        '99-cromnix.rules'
        'polkit-0.114-elogind.patch')
sha256sums=('SKIP'
            'd28d6ecaf7682860e2b3768bb266bd94313546b7cd0ef3c2eace7b1576c47adb'
            'e5eb83a8922f1540309ce5884c4bd40b2972aa75778da945e2392501961a860b')

pkgver() {
    cd $pkgname
    git describe --tags | sed 's/-/+/g'
}

prepare() {
    cd $pkgname #-$pkgver
    patch -Np 1 -i "${srcdir}"/polkit-0.114-elogind.patch
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd $pkgname #-$pkgver

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --sbindir=/usr/bin \
        --localstatedir=/var \
        --libexecdir=/usr/lib \
        --enable-libsystemd-login=no \
        --enable-libelogind=yes \
        --disable-static \
        --enable-gtk-doc \
        --with-authfw=pam \
        --with-os-type=gentoo
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
    cd $pkgname #-$pkgver
    make -k check || :
}

package() {
    cd $pkgname #-$pkgver
    make DESTDIR="$pkgdir" install

    chown root:102 "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
    chmod 750      "$pkgdir"/{etc,usr/share}/polkit-1/rules.d
    install -m0644 "${srcdir}"/99-cromnix.rules "${pkgdir}"/etc/polkit-1/rules.d
    
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf" <<END
u polkitd 102 "PolicyKit daemon"
m polkitd proc
END
}
