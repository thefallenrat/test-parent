# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=subversion
pkgver=1.9.7
pkgrel=5
pkgdesc="A Modern Concurrent Version Control System"
arch=('i686' 'x86_64')
url="http://subversion.apache.org/"
license=('APACHE')
depends=('sqlite' 'file' 'serf' 'eudev')
makedepends=('apache' 'python2' 'perl' 'swig' 'java-environment'
             'libgnome-keyring' 'kdelibs' 'ruby')
optdepends=('libgnome-keyring: for GNOME Keyring for auth credentials'
            'kdebase-runtime: for KWallet for auth credentials'
            'bash-completion: for svn bash completion'
            'python2: for some hook scripts'
            'java-environment: for Java support'
            'ruby: for some hook scripts')
provides=('svn')
backup=('etc/xinetd.d/svn')
options=('!makeflags' '!emptydirs')
source=(https://www.apache.org/dist/subversion/subversion-${pkgver}.tar.bz2{,.asc}
        svn
        svnserve.tmpfiles
        subversion.rpath.fix.patch
        ruby-frozen-nil.patch)
md5sums=('05b0c677681073920f938c1f322e0be2'
         'SKIP'
         'a0db6dd43af33952739b6ec089852630'
         'bb2857eceafcfac35dde39dcffad2314'
         '31c0f079a0b0815d6af5673ab7be96af'
         '6b1e5abe57edcc662582bcf05fee34bb')
validpgpkeys=('19BBCAEF7B19B280A0E2175E62D48FAD16A0DE01'
              '8BC4DAE0C5A4D65F404401074F7DBAA99A59B973'
              'BA3C15B1337CF0FB222BD41A1BCA6586A347943F'
              'E7B2A7F4EC28BE9FF8B38BA4B64FFF1209F9FA74'
              '3D1DC66D6D2E0B9039528138C4A6C625CCC8E1DF')
# Ben Reser / Stefan Sperling / Branko Čibej / Evgeny Kotkov / Bert Huijben

prepare() {
   cd ${pkgname}-${pkgver}
   patch -Np0 -i ../subversion.rpath.fix.patch
   patch -p1 -i ../ruby-frozen-nil.patch
   sed -i 's|/usr/bin/env python|/usr/bin/env python2|' tools/hook-scripts/{,mailer/{,tests/}}*.py subversion/tests/cmdline/*.py
}

build() {
   cd ${pkgname}-${pkgver}
   export PYTHON=/usr/bin/python2
   ./configure --prefix=/usr --with-apr=/usr --with-apr-util=/usr \
               --with-zlib=/usr --with-serf=/usr --with-apxs \
               --with-sqlite=/usr \
               --enable-javahl --with-jdk=/usr/lib/jvm/default \
               --with-gnome-keyring --with-kwallet \
               --with-apache-libexecdir=/usr/lib/httpd/modules \
               --with-ruby-sitedir=/usr/lib/ruby/vendor_ruby \
               --disable-static

   make LT_LDFLAGS="-L$Fdestdir/usr/lib"
   make swig_pydir=/usr/lib/python2.7/site-packages/libsvn \
     swig_pydir_extra=/usr/lib/python2.7/site-packages/svn swig-py swig-pl javahl swig-rb
}

check() {
   cd ${pkgname}-${pkgver}
   export LANG=C LC_ALL=C
   make check check-swig-pl check-swig-py check-swig-rb CLEANUP=yes # check-javahl
}

package() {
   cd ${pkgname}-${pkgver}

   export LD_LIBRARY_PATH="${pkgdir}"/usr/lib:${LD_LIBRARY_PATH}
   make DESTDIR="${pkgdir}" INSTALLDIRS=vendor \
     swig_pydir=/usr/lib/python2.7/site-packages/libsvn \
     swig_pydir_extra=/usr/lib/python2.7/site-packages/svn \
     install install-swig-py install-swig-pl install-javahl install-swig-rb

   install -dm755 "${pkgdir}"/usr/share/subversion
   cp -a tools/hook-scripts "${pkgdir}"/usr/share/subversion/
   rm "${pkgdir}"/usr/share/subversion/hook-scripts/*.in

   ## svnserve ...

   # xinetd
   install -D -m 644 "${srcdir}"/svn "${pkgdir}"/etc/xinetd.d/svn

   install -D -m 644 "${srcdir}"/svnserve.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/svnserve.conf

   # ... common config
#    install -D -m 644 "${srcdir}"/svnserve.conf "${pkgdir}"/etc/conf.d/svnserve

   install -Dm 644 tools/client-side/bash_completion \
     "${pkgdir}"/usr/share/bash-completion/completions/subversion
   for i in svn svnadmin svndumpfilter svnlook svnsync svnversion; do
      ln -sf subversion "${pkgdir}"/usr/share/bash-completion/completions/${i}
   done
}

